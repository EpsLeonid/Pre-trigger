TITLE "Phase_Switch";
-- This file contains a function for automatic switching between the clock 
-- generated by the local Quartz and the external clock 
-- Fmax       = 26000 ; -- Upper limit in kHz
-- Fmin       = 24000 ; -- Lower limit in kHz
-- RefClock   = 25000 ; -- Local Quartz Frequency in kHz
-- For information:  F(SysClk) = 2* F(VEPP's phase) = 24.64MHz

INCLUDE "lpm_counter.inc" ;

PARAMETERS
(
Fmax        = 26000,--25600 ,   -- Upper limit in kHz
Fmin        = 23000,--24000 ,   -- Lower limit in kHz
RefClock    = 25000     -- Local Quartz Freq(kHz) used as the reference
);

SUBDESIGN  PhaseSwitch
(
clock           : input;    -- reference clock from local Quartz
SysClk			: input;    -- Input of the Sys's clock "Phase0"
Reset           : input;    -- main project Reset, active High
	
Phase25         : output;   -- Output of the selected clock
SysClk_Selected	: output;   -- Signal indicates what Clk source is selected
Test[4..1]      : output;
Error			: output;
)

VARIABLE 

--	ClockDivider[1..0]  : DFF;
RefTimeCt		: LPM_COUNTER with (LPM_WIDTH = 16,	LPM_DIRECTION = "Up");
SysClkCt		: LPM_COUNTER with (LPM_WIDTH = 16,	LPM_DIRECTION = "Up");

RefTimeZero,
SysClkZero		: node;

CycleEnd		: node;
EndTrig[4..1]	: node;
Fnorm,
FnormTrig		: node;

BEGIN

DEFAULTS

Error = GND;

END DEFAULTS;

--============================================================================
--************  Frequency comparator  ****************************************
-- Base time interval :
RefTimeCt.clock = clock ;
RefTimeCt.sclr  = EndTrig4 OR Reset ;

IF (RefTimeCt.q[15..0] == RefClock)     -- Base time interval, 25000 <=> 1ms 
    Then  CycleEnd = VCC;
    Else  CycleEnd = GND;
End IF;
EndTrig1= SRFF(.S=CycleEnd, .clk=clock, -- 2-clk _/^^\_ on "End of Cycle"
                --.R=EndTrig4, .clrn=!Reset);
                .R=(!RefTimeZero AND !SysClkZero), .clrn=!Reset);
EndTrig2= DFF (.D=EndTrig1, .clk=clock, .clrn=!Reset);
EndTrig3= DFF (.D=EndTrig2, .clk=clock, .clrn=!Reset);
EndTrig4= DFF (.D=EndTrig3, .clk=clock, .clrn=!Reset);

RefTimeZero = DFF(.D=(RefTimeCt.q15 # RefTimeCt.q14 # RefTimeCt.q13 # RefTimeCt.q12 
                    # RefTimeCt.q11 # RefTimeCt.q10 # RefTimeCt.q9  # RefTimeCt.q8  
                    # RefTimeCt.q7  # RefTimeCt.q6  # RefTimeCt.q5  # RefTimeCt.q4  
                    # RefTimeCt.q3  # RefTimeCt.q2  # RefTimeCt.q1  # RefTimeCt.q0 ),
                  .clk=Clock );

SysClkZero = DFF(.D=(SysClkCt.q15 # SysClkCt.q14 # SysClkCt.q13 # SysClkCt.q12 
                    # SysClkCt.q11 # SysClkCt.q10 # SysClkCt.q9  # SysClkCt.q8  
                    # SysClkCt.q7  # SysClkCt.q6  # SysClkCt.q5  # SysClkCt.q4  
                    # SysClkCt.q3  # SysClkCt.q2  # SysClkCt.q1  # SysClkCt.q0 ),
                  .clk=SysClk );

-- Count the external clock frequency, in Base time interval :
SysClkCt.clock = SysClk ;
SysClkCt.sclr  = DFF(.D=(EndTrig4 OR Reset), .clk=SysClk) ;

IF ( (SysClkCt.q[15..0] > Fmin) AND (SysClkCt.q[15..0] < Fmax) )
    Then  Fnorm = VCC;
    Else  Fnorm = GND;
End IF;                 -- important: to avoid registration of an improper value, ..
FnormTrig   = DFFE(.D=Fnorm, .clk=SysClk,            -- .. clock must be = SysClk !
                .ena=DFF(.D=!EndTrig1, .clk=SysClk), -- .. clock must be = SysClk !
                .clrn=!Reset); 

SysClk_Selected = DFF(.D=FnormTrig, .clk = EndTrig3, .clrn=!Reset);

--============================================================================
-- MUX of clock signals : 
Phase25 = ((SysClk and SysClk_Selected) OR (Clock and !SysClk_Selected));

Test1 = FnormTrig;
Test2 = EndTrig1;
Test3 = EndTrig3;
Test4 = SysClkZero;

END;